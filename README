Examples:

package
{
	import as3spec.*;

	public class TestAS3Spec extends Spec
	{
		public function TestAS3Spec ()
		{
			describe ('as3spec', function () :void
			{
				const arr:Array = new Array;

				it ('should provide a basic suite of requirements', function () :void
				{
					// ==
					so(23).should.equal(23);
					// !=
					so(23).should.not.equal(15);

					// ===
					so(arr).should.be.same(arr);
					// !==
					so(arr).should.not.be.same((new Array));

					// == null
					var nothing:String;
					so(nothing).should.be.nil;
					// != null
					so(arr).should.not.be.nil;

					// =~
					so('hello').should.match(/ell/);
					// !=~
					so('hello').should.not.match(/egg/);

					// "var is Type"
					so(arr).should.be.a.kind_of(Array);
					so(arr).should.not.be.a.kind_of(Boolean);
				});

				it ('should trap an empty specification', function () :void
				{
				});

				it ('should trap an error', function () :void
				{
					so(arr);
					throw(new Error('catch me if you can!'));
				});

				it ('should trap a failure', function () :void
				{
					so(23).should.equal(15);
				});
			});

			describe ('describe()', 'with', 'arbitrary', 'arguments', function () :void
			{
				it ('should succeed', function () :void
				{
					so(true).should.equal(true);
				});
			});
		}
	}
}

package
{
	import as3spec.*;
	import mx.controls.*;
	import mx.core.*;

	public class TestUIComponentSpec extends UIComponentSpec
	{
		public function TestUIComponentSpec ()
		{
			const instance:UIComponent = new Button;

			describe (instance, 'is a component from the mx.controls package', function () :void
			{
				it ('should be a UIComponent', function () :void
				{
					so(instance).should.be.a.kind_of(UIComponent);
				});
				
				it ('should have "initialized" before this specification was run', function () :void
				{
					so(instance.initialized).should.equal(true);
				});
			});

			describe (instance, 'described()', 'with', 'arbitrary', 'arguments', function () :void
			{
				it ('should succeed', function () :void
				{
					so(true).should.equal(true);
				});
			});
		}
	}
}